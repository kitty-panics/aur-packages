# Maintainer: Kitty Panics

pkgname=wallpaper-windows11
pkgver=2023.06.22H2
pkgrel=1
url="https://next.itellyou.cn/Original/Index#cbp=Product?ID=42e87ac8-9cd6-eb11-bdf8-e0d4e850c9c6"
pkgdesc="MS Windows11 (22H2) wallpaper."
arch=('any')
license=('custom')
provides=("${pkgname}")
conflicts=("${pkgname}")
makedepends=("p7zip" "rsync")
_iso="$(ls -A | grep -i '.iso$')"
source=("${_iso}")
sha256sums=('16ea95e1818c91fde6c5b8b0c8c34bebc06fee572db2b3fc5090cadbd7fa7be0')

prepare() {
    echo " - Extracting Windows installation image"
    7z e "${source}" sources/install.wim

    echo " - Extracting files from local Windows installation image"
    7z x install.wim 5/Windows/Web "${srcdir}"/

    # set the correct permissions
    find "${srcdir}"/5/Windows/Web -type d -exec chmod 755 {} \;
    find "${srcdir}"/5/Windows/Web -type f -exec chmod 644 {} \;
}

package() {
    # create dir
    install -dm755 "${pkgdir}"/usr/share/backgrounds/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}"/5/Windows/Web/Wallpaper/ \
               "${pkgdir}"/usr/share/backgrounds/"${pkgname}"/ \
               --exclude="Desktop.ini" --exclude="Extended" --exclude="img50.jpg"
    rsync -rtl "${srcdir}"/5/Windows/Web/touchkeyboard \
               "${pkgdir}"/usr/share/backgrounds/"${pkgname}"/
    rsync -rtl "${srcdir}"/5/Windows/Web/Screen \
               "${pkgdir}"/usr/share/backgrounds/"${pkgname}"/ \
               --exclude="img102.jpg" --exclude="img105.jpg"
    # symbolic links
    cd "${pkgdir}"/usr/share/backgrounds/"${pkgname}"/
    find . -type f | while read i; do
        ln -s /usr/share/backgrounds/"${pkgname}"/"${i:2}" \
              "${pkgdir}"/usr/share/backgrounds/"${pkgname}"/
    done
}
