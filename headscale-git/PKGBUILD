# Maintainer: Kitty Panics
# Upstream: https://gitlab.archlinux.org/archlinux/packaging/packages/headscale
#
# Contributor: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Matt Harrison <matt@harrison.us.com>

    #> Package name
#pkgbase=
pkgname=headscale-git
    #> Version
pkgver=r3033.1e22f17f36
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="An open source, self-hosted implementation of the Tailscale control server."
arch=('x86_64')
url="https://github.com/juanfont/headscale"
license=('BSD')
    #> Dependencies
#depends=('')
makedepends=('go')
optdepends=('wireguard-tools: CLI tools for generating keys'
            'postgresql: alternative database provider')
    #> Package relations
provides=("${pkgname}" "${pkgname%-*}")
conflicts=("${pkgname}" "${pkgname%-*}")
    #> Others
backup=("etc/${pkgname%-*}/config.yaml"
        "etc/${pkgname%-*}/derp.yaml")
#options=('')
#install=xyz.install
    #> Sources
source=("git+${url}.git"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
#noextract=("")
    #> Integrity
sha256sums=('SKIP'
            '059353f4843dec6eb447c567fac890ef63cc9c8acea18840fcfc3f4a76d596db'
            '8a22d7193ceeac0be32725cf8108f963be3a21855e6099de964f810094d0adc7')

pkgver() {
    print-github-repo-version "${url}"
}

build() {
    cd "${srcdir}"/"${pkgname%-*}"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    go build -tags="ts2021" -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -s -w" ./cmd/headscale
}

package() {
    cd "${srcdir}"/"${pkgname%-*}"

    # /usr/bin/
    install -Dm755 "${pkgname%-*}" "${pkgdir}/usr/bin/${pkgname%-*}"

    # config files
    install -Dm644 "config-example.yaml" "${pkgdir}/etc/${pkgname%-*}/config.yaml"
    install -Dm644 "derp-example.yaml"   "${pkgdir}/etc/${pkgname%-*}/derp.yaml"
    install -Dm644 "config-example.yaml" "${pkgdir}/usr/share/${pkgname}/config-example.yaml"
    install -Dm644 "derp-example.yaml"   "${pkgdir}/usr/share/${pkgname}/derp-example.yaml"

    # /usr/lib/
    install -Dm644 "${srcdir}/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
    install -Dm644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"

    # systemd .service
    install -Dm644 "docs/packaging/headscale.systemd.service" "${pkgdir}/usr/lib/systemd/system/${pkgname%-*}.service"

    # licenses
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
