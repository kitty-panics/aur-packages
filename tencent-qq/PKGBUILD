# Maintainer: Kitty Panics

    #> Package name
#pkgbase=
pkgname=tencent-qq
    #> Version
pkgver=3.2.5_240305
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="An instant messaging software developed by Tencent."
arch=('x86_64')
url="https://im.qq.com/rainbow/linuxQQDownload/"
license=('unknown')
    #> Dependencies
#depends=('')
#makedepends=('')
optdepends=('libappindicator-gtk3: Allow applications to extend a menu via Ayatana indicators in Unity, KDE or Systray')
    #> Package relations
provides=("${pkgname}")
conflicts=("${pkgname}")
    #> Others
#backup=("")
#options=('')
#install=xyz.install
    #> Sources
source=("https://dldir1.qq.com/qqfile/qq/QQNT/Linux/QQ_3.2.5_240305_amd64_01.deb"
        "${pkgname}.desktop")
#noextract=("")
    #> Integrity
sha256sums=('SKIP'
            'bb8ae7c08ce017d4703e3dc1968ccaa8f7db4296d600cbe67ab3edbd68dd544e')

prepare() {
    tar -xJf "${srcdir}"/data.tar.xz
}

package() {
    # mkdir
    install -dm755 "${pkgdir}"/opt/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/bin
    install -dm755 "${pkgdir}"/usr/share/applications
    install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/512x512/apps
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"

    # pkg files
    cp -a "${srcdir}"/opt/QQ/* \
          "${pkgdir}"/opt/"${pkgname}"/
    # bin
    ln -s /opt/"${pkgname}"/qq \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    install -Dm644 "${srcdir}"/usr/share/doc/linuxqq/changelog.gz \
                   "${pkgdir}"/usr/share/doc/"${pkgname}"/
    # icons
    install -Dm644 "${srcdir}"/usr/share/icons/hicolor/512x512/apps/qq.png \
                   "${pkgdir}"/usr/share/icons/hicolor/512x512/apps/"${pkgname}".png
    # licenses
    mv "${pkgdir}"/opt/"${pkgname}"/LICENSE.electron.txt \
       "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    mv "${pkgdir}"/opt/"${pkgname}"/LICENSES.chromium.html \
       "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
