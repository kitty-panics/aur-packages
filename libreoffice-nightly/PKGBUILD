# Maintainer: Kitty Panics

pkgname=libreoffice-nightly
pkgver=24.2.0.0.alpha0
pkgrel=20230703
url="https://dev-builds.libreoffice.org/daily/master/Linux-rpm_deb-x86_64@tb87-TDF/current/"
pkgdesc="LibreOffice development branch"
arch=('x86_64')
license=('LGPL3')
provides=("${pkgname}" "libreoffice")
conflicts=("${pkgname}" "libreoffice" "libreoffice-fresh" "libreoffice-still")
depends=('nspr' 'nss' 'gstreamer' 'kwindowsystem' 'orc' 'qt5-x11extras')
optdepends=('java-runtime:    adds java support'
            'pstoedit:        translates PostScript and PDF graphics into other vector formats'
            'libmythes:       for use in thesaurus'
            'beanshell:       interactive java -- good for prototyping/macros'
            'libwpg:          library for importing and converting WordPerfect Graphics format'
            'sane:            for scanner access'
            'unixodbc:        adds ODBC database support'
            'gst-plugins-base-libs: for multimedia content, e.g. in Impress'
            'libpaper:        takes care of papersize'
            'postgresql-libs: for postgresql-connector'
            'mariadb-libs:    for mysql-connector'
            'coin-or-mp:      required by the Calc solver'
            'gtk3:            for GTK3 integration'
            'gtk4:            or GTK4 integration (experimental)'
            'kio:             for KF5 KDE desktop integration'
            'qt6-base:        for QT6 desktop integration')
makedepends=('dpkg')
source=("${url}/LibreOfficeDev_${pkgver}_Linux_x86-64_deb.tar.gz"
        "${url}/LibreOfficeDev_${pkgver}_Linux_x86-64_deb_helppack_en-US.tar.gz")
#       "${url}/LibreOfficeDev_${pkgver}_Linux_x86-64_deb_helppack_zh-CN.tar.gz"
#       "${url}/LibreOfficeDev_${pkgver}_Linux_x86-64_deb_langpack_zh-CN.tar.gz")
sha256sums=('SKIP'
            'SKIP')
#           'SKIP'
#           'SKIP')

prepare() {
    find "${srcdir}"/*/*/*.deb -type f -exec dpkg -x {} "${srcdir}" \;
}

package() {
    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgname}"/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"/
    for i in 16x16 32x32 64x64 128x128 256x256 512x512 scalable; do
        install -dm755 "${pkgdir}"/usr/share/icons/hicolor/"${i}"/apps/
    done
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    install -dm755 "${pkgdir}"/usr/share/menu/
    install -dm755 "${pkgdir}"/usr/share/metainfo/
    install -dm755 "${pkgdir}"/usr/share/mime/packages/

    # package files
    rsync -rtl "${srcdir}"/opt/libreofficedev"${pkgver%%.0*}"/ \
               "${pkgdir}"/opt/"${pkgname}"
    # /usr/bin/
    ln -s "/opt/${pkgname}/program/soffice" \
          "${pkgdir}/usr/bin/${pkgname}"
    ln -s "/opt/${pkgname}/program/soffice" \
          "${pkgdir}/usr/bin/soffice"

    # .desktop
    for i in `ls "${pkgdir}"/opt/"${pkgname}"/share/xdg/`; do
        install -Dm644 "${pkgdir}"/opt/"${pkgname}"/share/xdg/"${i}" \
                       "${pkgdir}"/usr/share/applications/"${pkgname}"-"${i}"
    done
    sed -i 's/dev"${pkgver%%.0*}"/-nightly/g' "${pkgdir}"/usr/share/applications/*.desktop
    sed -i 's/Dev "${pkgver%%.0*}"/ Nightly/g' "${pkgdir}"/usr/share/applications/*.desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"/
    ln -s /opt/"${pkgname}"/readmes/README_en-US \
          "${pkgdir}"/usr/share/doc/"${pkgname}"/
    # icons/hicolor
    _soffice_list="base calc chart draw impress main math startcenter writer"
    for i in 16x16 32x32 64x64 128x128 256x256 512x512; do
        for j in `echo $_soffice_list`; do
            install -Dm644 "${srcdir}"/usr/share/icons/hicolor/"${i}"/apps/libreofficedev"${pkgver%%.0*}"-"${j}".png \
                           "${pkgdir}"/usr/share/icons/hicolor/"${i}"/apps/"${pkgname}"-"${j}".png
        done
    done
    for i in `echo $_soffice_list`; do
        install -Dm644 "${srcdir}"/usr/share/icons/hicolor/scalable/apps/libreofficedev"${pkgver%%.0*}"-"${i}".svg \
                       "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}"-"${i}".svg
    done
    # licenses
    ln -s /opt/"${pkgname}"/LICENSE \
          "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    ln -s /opt/"${pkgname}"/LICENSE.html \
          "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    ln -s /opt/"${pkgname}"/NOTICE \
          "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    # menu
    install -Dm644 "${srcdir}"/usr/lib/menu/libreofficedev"${pkgver%%.0*}" \
                   "${pkgdir}"/usr/share/menu/"${pkgname}".menu
    sed -i 's/Dev "${pkgver%%.0*}"/ Nightly/g' "${pkgdir}"/usr/share/menu/"${pkgname}".menu
    sed -i 's/libreoffice-/libreoffice-nightly-/g' "${pkgdir}"/usr/share/menu/"${pkgname}".menu
    sed -i 's/dev"${pkgver%%.0*}"/-nightly/g' "${pkgdir}"/usr/share/menu/"${pkgname}".menu
    # metainfo
    for i in `ls "${srcdir}"/usr/share/metainfo/`; do
        install -Dm644 "${srcdir}"/usr/share/metainfo/"${i}" \
                       "${pkgdir}"/usr/share/metainfo/"${i//dev"${pkgver%%.0*}"/-nightly}"
    done
    sed -i 's/libreoffice-/libreoffice-nightly-/g' "${pkgdir}"/usr/share/metainfo/*.xml
    sed -i 's/LibreOffice /LibreOffice Nightly /g' "${pkgdir}"/usr/share/metainfo/*.xml
    # mime
    install -Dm644 "${srcdir}"/usr/share/mime/packages/libreofficedev"${pkgver%%.0*}".xml \
                   "${pkgdir}"/usr/share/mime/packages/"${pkgname}".xml

    # useless files
    #
    # xdg
    rm -r "${pkgdir}"/opt/"${pkgname}"/share/xdg
    # fonts
    for i in `find "${pkgdir}"/opt/libreoffice-nightly/share/fonts/truetype -type f | sed '/fc_local.conf/d;/opens___.ttf/d'`; do
        rm -r "${i}"
    done
    sed -i '10,38d' "${pkgdir}"/opt/libreoffice-nightly/share/fonts/truetype/fc_local.conf
}
