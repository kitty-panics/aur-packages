# Maintainer: Kitty Panics

    #> Package name
#pkgbase=
pkgname=zwsoft-zwcad2024
    #> Version
pkgver=24.1.3.4
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="基于自主内核的国产二维 CAD 平台."
arch=('x86_64')
url="https://www.zwsoft.cn/product/zwcad/linux"
license=('unknown')
    #> Dependencies
#depends=('')
#makedepends=('')
#optdepends=('')
    #> Package relations
provides=("${pkgname}" "zwcad2024")
conflicts=("${pkgname}" "zwcad2024")
    #> Others
#backup=("")
#options=('')
#install=xyz.install
    #> Sources
_key="1706461475-0-0-d54c058cfa01eca3640e8c4f7fc3fb0d"
source=("zwcad2024_24.1.3.4_amd64.deb::https://downloads.zwsoft.cn/zwcad/cad_linux/2024/SP/${pkgname}_${pkgver}_amd64.deb?auth_key=${_key}")
#noextract=("")
    #> Integrity
sha256sums=('SKIP')

prepare() {
    tar -xJf "${srcdir}"/data.tar.xz
}

package() {
    # mkdir
    install -dm755 "${pkgdir}"/opt/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/bin
    install -dm755 "${pkgdir}"/usr/share/applications
    install -dm755 "${pkgdir}"/usr/share/icons
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/share/mime/packages

    # pkg files
    cp -a "${srcdir}"/opt/apps/zwcad2024/* \
          "${pkgdir}"/opt/"${pkgname}"/
    # bin
    ln -s /opt/"${pkgname}"/ZWCADRUN.sh \
          "${pkgdir}"/usr/bin/
    ln -s /opt/"${pkgname}"/ZwCloud2DRun.sh \
          "${pkgdir}"/usr/bin/
    ln -s /opt/"${pkgname}"/ZWLMGRRUN.sh \
          "${pkgdir}"/usr/bin/
    # fix: "libGL error: No matching fbConfigs or visuals found"
    # fix: "libGL error: failed to load driver: swrast"
    sed -i 's|:$(dirname $0)/lib/GL||' "${pkgdir}"/opt/"${pkgname}"/ZWCADRUN.sh

    # .desktop
    install -Dm644 "${srcdir}"/usr/share/applications/*.desktop \
                -t "${pkgdir}"/usr/share/applications/
    find "${pkgdir}"/usr/share/applications/ -type f -exec sed -i 's|sh /opt/apps/zwcad2024|/usr/bin|' {} \;
    # icons
    cp -a "${srcdir}"/usr/share/icons/* \
          "${pkgdir}"/usr/share/icons/
    # licenses
    mv "${pkgdir}"/opt/"${pkgname}"/ThirdPartyLicense \
       "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    ln -s /opt/"${pkgname}"/License.txt \
          "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    # mime
    install -Dm644 "${srcdir}"/usr/share/mime/packages/zwcad2024.xml \
                   "${pkgdir}"/usr/share/mime/packages/
}
