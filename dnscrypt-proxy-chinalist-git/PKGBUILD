# Maintainer: Kitty Panics

pkgname=dnscrypt-proxy-chinalist-git
pkgver=r164383.4cf73bbb00
pkgrel=1
url="https://github.com/felixonmars/dnsmasq-china-list"
pkgdesc="Chinese-specific configuration to improve dnscrypt-proxy."
arch=('any')
license=('custom:DWTFYWT')
provides=("${pkgname}" "${pkgname%-*}")
conflicts=("${pkgname}" "${pkgname%-*}")
depends=('dnscrypt-proxy')
source=("${url}/raw/master/accelerated-domains.china.conf"
        "${url}/raw/master/LICENSE"
        'Makefile')
sha256sums=('SKIP'
            'SKIP'
            '882a8d8c0db184be031ba59464fd8e3cd029d6e7af0c5fd99e276ce73a2bca03')

pkgver() {
    _repo_url="https://api.github.com/repos/$(echo $url | cut -d'/' -f4,5)"
    _commit_count=$(curl -sLI "${_repo_url}/commits?per_page=1" | grep '^link' | cut -d'=' -f6 | cut -d'>' -f1)
    _last_commit=$(curl -sL "${_repo_url}/git/refs/heads/master" | grep 'sha' | cut -d'"' -f4 | cut -c 1-10)
    printf "r%s.%s" "${_commit_count}" "${_last_commit}"
}

build() {
    DEDICATED_SERVER='223.6.6.6'
    make SERVER="$DEDICATED_SERVER" dnscrypt-proxy
}

package() {
    # mkdir
    install -dm755 "${pkgdir}"/etc/dnscrypt-proxy
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"

    # pkg files
    install -Dm644 "${srcdir}"/dnscrypt-proxy-forwarding-rules.txt \
                   "${pkgdir}"/etc/dnscrypt-proxy/chinalist.txt

    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
