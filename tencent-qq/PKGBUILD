# Maintainer: Kitty Panics

pkgname=tencent-qq
pkgver=3.2.5_20811
pkgrel=1
url="https://im.qq.com/rainbow/linuxQQDownload/"
pkgdesc="An instant messaging software developed by Tencent."
arch=('x86_64')
license=('unknown')
provides=("${pkgname}")
conflicts=("${pkgname}")
optdepends=('libappindicator-gtk3: Allow applications to extend a menu via Ayatana indicators in Unity, KDE or Systray')
makedepends=('imagemagick')
source=("https://dldir1.qq.com/qqfile/qq/QQNT/d0154345/linuxqq_${pkgver/_/-}_amd64.deb"
        "${pkgname}.desktop")
sha256sums=('SKIP'
            'bb8ae7c08ce017d4703e3dc1968ccaa8f7db4296d600cbe67ab3edbd68dd544e')

prepare() {
    tar -xJf "${srcdir}"/data.tar.xz
}

package() {
    # mkdir
    install -dm755 "${pkgdir}"/opt/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/bin
    install -dm755 "${pkgdir}"/usr/share/applications
    install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/512x512/apps
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"

    # pkg files
    cp -a "${srcdir}"/opt/QQ/* \
          "${pkgdir}"/opt/"${pkgname}"/
    # bin
    ln -s "/opt/${pkgname}/qq" \
          "${pkgdir}/usr/bin/${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    install -Dm644 "${srcdir}"/usr/share/doc/linuxqq/changelog.gz \
                   "${pkgdir}"/usr/share/doc/"${pkgname}"/
    # icons
    install -Dm644 "${srcdir}"/usr/share/icons/hicolor/512x512/apps/qq.png \
                   "${pkgdir}"/usr/share/icons/hicolor/512x512/apps/"${pkgname}".png
    # licenses
    mv "${pkgdir}"/opt/"${pkgname}"/LICENSE.electron.txt \
       "${pkgdir}"/usr/share/licenses/"${pkgname}"/
    mv "${pkgdir}"/opt/"${pkgname}"/LICENSES.chromium.html \
       "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
