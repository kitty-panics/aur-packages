# Maintainer: Kitty Panics

    #> Package name
#pkgbase=
pkgname=n3n-git
    #> Version
pkgver=r1729.752d6a8357
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="Peer-to-peer VPN."
arch=('x86_64')
url="https://github.com/hamishcoleman/n3n" # n42n/n3n
license=('GPL3')
    #> Dependencies
#depends=('')
makedepends=('zstd' 'openssl' 'libcap' 'libpcap' 'libnatpmp' 'miniupnpc')
optdepends=('python: for n3n-ctl, n3n-httpd command')
    #> Package relations
provides=("${pkgname}" "${pkgname%-*}" "n2n-git" "n2n")
conflicts=("${pkgname}" "${pkgname%-*}" "n2n-git" "n2n")
    #> Others
backup=('etc/n3n/edge.conf'
        'etc/n3n/supernode.conf')
#options=('')
#install=xyz.install
    #> Sources
source=("git+${url}.git"
        'full-edge.conf'
        'full-supernode.conf'
        "n3n-git.tmpfiles"
        'version.sh')
#noextract=("")
    #> Integrity
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
    print-github-repo-version "${url}"
}

prepare() {
    cd "${srcdir}"/"${pkgname%-*}"

    # fix: sbin -> bin
    sed -i 's|/sbin|/bin|' Makefile
    # fix: Error: VERSION file does not match tag version (3.1.1 != )
    cp -f "${srcdir}"/version.sh scripts/version.sh
    # fix: Welcome to n3n v.
    sed -i '/Welcome to n3n/ s| v| |' src/n2n.c
}

build() {
    cd "${srcdir}"/"${pkgname%-*}"
    ./autogen.sh
    ./configure --with-zstd --with-openssl \
                --enable-cap --enable-pcap \
                --enable-natpmp --enable-miniupnp
    # fix: "src/management.c:317:27: error: expected ‘;’ before ‘}’ token"
    sed -i '/200)$/s/$/;/' src/management.c
    make
}

package() {
    # mkdir
    install -dm755 "${pkgdir}"/usr/bin
    install -dm755 "${pkgdir}"/usr/lib/systemd/system
    install -dm755 "${pkgdir}"/usr/share/man/man{1,7,8}
    install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"

    # pkg files, bin, man
    cd "${srcdir}"/"${pkgname%-*}"
    DESTDIR="${pkgdir}" make install
    install -Dm755 scripts/n3n-{ctl,httpd} \
                -t "${pkgdir}"/usr/bin
    # etc
    install -Dm644 "${srcdir}"/full-edge.conf \
                   "${pkgdir}"/etc/n3n/edge.conf
    install -Dm644 "${srcdir}"/full-supernode.conf \
                   "${pkgdir}"/etc/n3n/supernode.conf

    # .service
    install -Dm644 packages/lib/systemd/system/* \
                -t "${pkgdir}"/usr/lib/systemd/system/
    find "${pkgdir}"/usr/lib/systemd/system/ -type f -exec sed -i 's|/sbin|/bin|g' {} \;
    sed -i '/User=/c\User=nobody'   "${pkgdir}"/usr/lib/systemd/system/supernode.service
    sed -i '/Group=/c\Group=nobody' "${pkgdir}"/usr/lib/systemd/system/supernode.service
    # doc
    install -Dm644 doc/*.md \
                -t "${pkgdir}"/usr/share/doc/"${pkgname}"/
    # licenses
    install -Dm644 LICENSES/preferred/* \
                -t "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
