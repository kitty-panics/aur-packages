# Maintainer: Kitty Panics

pkgbase=aria-ng
pkgname=(${pkgbase} ${pkgbase}-allinone)
pkgver=1.3.7
pkgrel=1
url="https://github.com/mayswind/AriaNg/releases"
pkgdesc="A modern web frontend making aria2 easier to use."
arch=('any')
license=('MIT')
depends=('aria2')
makedepends=('p7zip' 'rsync')
optdepends=('apache: A supported application server'
            'nginx: A supported application server'
            'nginx-mainline: A supported application server'
            'caddy: A supported application server'
            'lighttpd: A supported application server')
source=("${url}/download/${pkgver}/AriaNg-${pkgver}.zip"
        "${url}/download/${pkgver}/AriaNg-${pkgver}-AllInOne.zip")
sha256sums=('SKIP'
            'SKIP')
noextract=("AriaNg-${pkgver}.zip" "AriaNg-${pkgver}-AllInOne.zip")

prepare() {
    7z x -aos "AriaNg-${pkgver}.zip" -o"${srcdir}"/AriaNg
    7z x -aos "AriaNg-${pkgver}-AllInOne.zip" -o"${srcdir}"/AriaNg-AllInOne
    # set the correct permissions
    find "${srcdir}" -type d -exec chmod 755 {} \;
    find "${srcdir}" -type f -exec chmod 644 {} \;
}

package_aria-ng() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/usr/share/"${pkgname}"/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}"/AriaNg/ \
               "${pkgdir}"/usr/share/"${pkgname}" \
               --exclude="LICENSE"

    # licenses
    install -Dm644 "${srcdir}"/AriaNg/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

package_aria-ng-allinone() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/usr/share/"${pkgname}"/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    install -Dm644 "${srcdir}"/AriaNg-AllInOne/index.html \
                   "${pkgdir}"/usr/share/"${pkgname}"/

    # licenses
    install -Dm644 "${srcdir}"/AriaNg-AllInOne/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
