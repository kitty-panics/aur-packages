# Maintainer: Kitty Panics

    #> Package name
#pkgbase=
pkgname=n3n-git
    #> Version
pkgver=r2073.81e340fd3a
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="Peer-to-peer VPN."
arch=('x86_64')
url="https://github.com/hamishcoleman/n3n" # n42n/n3n
license=('GPL3')
    #> Dependencies
#depends=('')
makedepends=('zstd' 'openssl' 'libcap' 'libpcap' 'libnatpmp' 'miniupnpc')
optdepends=('python: for n3n-ctl, n3n-httpd command')
    #> Package relations
provides=("${pkgname}" "${pkgname%-*}" "n2n-git" "n2n")
conflicts=("${pkgname}" "${pkgname%-*}" "n2n-git" "n2n")
    #> Others
backup=('etc/n3n/edge.conf'
        'etc/n3n/supernode.conf')
#options=('')
#install=xyz.install
    #> Sources
source=("git+${url}.git"
        'full-edge.conf'
        'full-supernode.conf'
        'version.sh')
#noextract=("")
    #> Integrity
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
    print-github-repo-version "${url}"
}

prepare() {
    cd "${srcdir}"/"${pkgname%-*}"

    # fix: sbin -> bin
    sed -i 's|/sbin|/bin|' Makefile
    # fix: Error: VERSION file does not match tag version (3.1.1 != )
    cp -f scripts/version.sh scripts/version.sh.bak
    cp -f "${srcdir}"/version.sh scripts/version.sh
    # fix: Welcome to n3n v.
    sed -i '/n3n v/ s| v| |' src/n2n.c
}

build() {
    cd "${srcdir}"/"${pkgname%-*}"
    ./autogen.sh
    ./configure --prefix=/usr \
                --with-systemddir=/usr/lib/systemd/system \
                --docdir=/usr/share/doc/"${pkgname}" \
                --with-zstd --with-openssl \
                --enable-cap --enable-pcap \
                --enable-natpmp --enable-miniupnp
    make
}

package() {
    # mkdir
    install -dm755 "${pkgdir}"/etc/"${pkgname%-*}"
    install -dm755 "${pkgdir}"/usr/bin
    install -dm755 "${pkgdir}"/usr/lib/systemd/system
    install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"
    install -dm755 "${pkgdir}"/usr/share/man/man{7,8}
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"

    # pkg files, bin, man
    cd "${srcdir}"/"${pkgname%-*}"
    DESTDIR="${pkgdir}" make install
    # bin
    mv "${pkgdir}"/usr/bin/n3nctl \
       "${pkgdir}"/usr/bin/n3n-ctl
    # etc
    install -Dm644 "${srcdir}"/full-edge.conf \
                   "${pkgdir}"/etc/n3n/edge.conf
    install -Dm644 "${srcdir}"/full-supernode.conf \
                   "${pkgdir}"/etc/n3n/supernode.conf

    # .service
    find "${pkgdir}"/usr/lib/systemd/system/ -type f -exec sed -i 's|/sbin|/bin|g' {} \;
    sed -i '/^User=/d'  "${pkgdir}"/usr/lib/systemd/system/n3n-supernode.service
    sed -i '/^Group=/d' "${pkgdir}"/usr/lib/systemd/system/n3n-supernode.service
    # licenses
    install -Dm644 LICENSES/preferred/* \
                -t "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
