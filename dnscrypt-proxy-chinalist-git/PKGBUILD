# Maintainer: Kitty Panics

pkgname=dnscrypt-proxy-chinalist-git
pkgver=r157702.f59b9f0608
pkgrel=1
url="https://github.com/felixonmars/dnsmasq-china-list"
pkgdesc="Chinese-specific configuration to improve dnscrypt-proxy."
arch=('any')
license=('custom:DWTFYWT')
provides=("${pkgname}" "${pkgname:0:-4}")
conflicts=("${pkgname}" "${pkgname:0:-4}")
depends=('dnscrypt-proxy')
install="${pkgname}.install"
source=('https://github.com/felixonmars/dnsmasq-china-list/raw/master/accelerated-domains.china.conf'
        "${pkgname}.install"
        'https://github.com/felixonmars/dnsmasq-china-list/raw/master/LICENSE'
        'Makefile')
sha256sums=('SKIP'
            'bbcd9487612c36c2bdeb00db7413cb3367c8093a29d6e8e695953394060677d1'
            'SKIP'
            '882a8d8c0db184be031ba59464fd8e3cd029d6e7af0c5fd99e276ce73a2bca03')

pkgver() {
    _repo_url="https://api.github.com/repos/$(echo $url | cut -d'/' -f4,5)"
    _commit_count=$(curl -sLI "${_repo_url}/commits?per_page=1" | grep '^link' | cut -d'=' -f6 | cut -d'>' -f1)
    _last_commit=$(curl -sL "${_repo_url}/git/refs/heads/master" | grep 'sha' | cut -d'"' -f4 | cut -c 1-10)
    printf "r%s.%s" "${_commit_count}" "${_last_commit}"
}

build() {
    DEDICATED_SERVER='223.5.5.5'
    make SERVER="$DEDICATED_SERVER" dnscrypt-proxy
}

package() {
    # create dir
    install -dm755 "${pkgdir}"/etc/dnscrypt-proxy/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    install -Dm644 "${srcdir}"/dnscrypt-proxy-forwarding-rules.txt \
                   "${pkgdir}"/etc/dnscrypt-proxy/chinalist.txt

    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}
