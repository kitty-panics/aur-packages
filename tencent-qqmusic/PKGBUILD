# Maintainer: Kitty Panics

pkgname=tencent-qqmusic
pkgver=1.1.5
pkgrel=10
url="https://y.qq.com/download/download.html"
pkgdesc="A music platform of Tencent."
arch=('x86_64')
license=('unknown')
provides=("${pkgname}")
conflicts=("${pkgname}")
depends=('nspr' 'nss' 'alsa-lib' 'libxss')
makedepends=('dpkg')
source=("https://dldir1.qq.com/music/clntupate/linux/deb/qqmusic_${pkgver}_amd64.deb"
        "${pkgname}.desktop"
        "${pkgname}.sh")
sha256sums=('SKIP'
            '0a5410f6ebbeb457c373338a1260f87a0b0da1969f77e23620aa0296361a5590'
            '4730edef46f8cfb87161fb860082fe7d423bb94dd70202c0cb07785af5e0441e')
noextract=("qqmusic_${pkgver}_amd64.deb")

prepare() {
    dpkg -x "${srcdir}"/qqmusic_"${pkgver}"_amd64.deb "${srcdir}"/
}

package(){
    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgname}"/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"/
    for i in 16 32 64 128 256; do
        install -dm755 "${pkgdir}"/usr/share/icons/hicolor/"${i}"x"${i}"/apps/
    done

    # package files
    rsync -rtl "${srcdir}"/opt/qqmusic/ "${pkgdir}"/opt/"${pkgname}"
    # /usr/bin/
    install -Dm755 "${srcdir}"/"${pkgname}".sh \
                   "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    install -Dm644 "${srcdir}"/usr/share/doc/qqmusic/changelog.gz \
                   "${pkgdir}"/usr/share/doc/"${pkgname}"/
    # icons/hicolor
    for i in 16 32 64 128 256; do
        install -Dm644 "${srcdir}"/usr/share/icons/hicolor/"${i}"x"${i}"/apps/qqmusic.png \
                       "${pkgdir}"/usr/share/icons/hicolor/"${i}"x"${i}"/apps/"${pkgname}".png
    done
}
