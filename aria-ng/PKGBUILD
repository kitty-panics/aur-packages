# Maintainer: Kitty Panics

pkgbase=aria-ng
pkgname=(${pkgbase} ${pkgbase}-allinone)
pkgver=1.3.6
pkgrel=1
url="https://github.com/mayswind/AriaNg/releases"
pkgdesc="A modern web frontend making aria2 easier to use."
arch=('any')
license=('MIT')
depends=('aria2')
makedepends=("p7zip" "rsync")
optdepends=('apache: A supported application server'
            'nginx: A supported application server'
            'nginx-mainline: A supported application server'
            'caddy: A supported application server'
            'lighttpd: A supported application server')
source=("${url}/download/${pkgver}/AriaNg-${pkgver}.zip"
        "${url}/download/${pkgver}/AriaNg-${pkgver}-AllInOne.zip")
sha256sums=('2d36e1a39d95867b8e0cdb3cde96d04d40117bd37e8742d639da92496e07cc7b'
            '101504ab56072bfd42e9b58ef8abcfba1cb0eddcd4d9257ee011e0254616a720')
noextract=("AriaNg-${pkgver}.zip" "AriaNg-${pkgver}-AllInOne.zip")

prepare() {
    7z x "AriaNg-${pkgver}.zip" -o"${srcdir}"/AriaNg
    7z x "AriaNg-${pkgver}-AllInOne.zip" -o"${srcdir}"/AriaNg-AllInOne
}

package_aria-ng() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/usr/share/"${pkgname}"/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}"/AriaNg/ \
               "${pkgdir}"/usr/share/"${pkgname}" \
               --exclude="LICENSE"

    # licenses
    install -Dm644 "${srcdir}"/AriaNg/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_aria-ng-allinone() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/usr/share/"${pkgname}"/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    install -Dm644 "${srcdir}"/AriaNg-AllInOne/index.html \
                   "${pkgdir}"/usr/share/"${pkgname}"/

    # licenses
    install -Dm644 "${srcdir}"/AriaNg-AllInOne/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
